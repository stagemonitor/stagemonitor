/*
 * RELEASING A NEW VERSION
 *  - set the following properties in ~/.gradle/gradle.properties
 *    - nexusUsername=<username>
 *    - nexusPassword=<pwd>
 *    - signing.secretKeyRingFile=<file>
 *    - signing.password=<passphrase>
 *  - Execute ./release.sh. This is what will happen:
 *    - all tests are executed locally
 *    - you will be asked for the version number of the release and the next development (SNAPSHOT) version
 *    - the gradle release plugin will then tag and push the release tag to github
 *    - checks out the just created release tag e.g. '0.11.0'
 *    - executes 'gradle uploadArchives'
 *  - Release the deployment at https://oss.sonatype.org/#welcome
 *    more information is available under http://central.sonatype.org/pages/releasing-the-deployment.html
 *
 */

import groovy.json.StringEscapeUtils

buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}

	dependencies {
		classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
		classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.22.0'
	}
}

plugins {
	id 'net.researchgate.release' version '2.6.0'
	id "net.saliman.cobertura" version "2.6.1"
	id "org.owasp.dependencycheck" version "5.1.0"
}

repositories {
	mavenCentral()
}

apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'java'
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'

ext {
	metricsVersion = '3.2.4'
	slf4jVersion = '1.7.25'
	jacksonVersion = '2.9.6'
	esVersion = '6.2.4'
	byteBuddyVersion = '1.10.1'
	opentracingVersion = '0.33.0'
	jaegerVersion = '0.35.5'
	jettyVersion = '9.3.13.v20161014'
	getProjectsToRelease = this.&getProjectsToRelease
}

subprojects {
	group = "org.stagemonitor"

	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'java'
	apply plugin: 'cobertura'

	if (projectsToRelease.contains(project)) {
		apply plugin: 'osgi'
		apply from: "$rootDir/gradle/publish-maven.gradle"

		jar {
			manifest {
				attributes("Implementation-Version": version)
				instruction 'Bundle-Vendor', 'iSYS Software GmbH'
				instruction 'Bundle-Description', 'Open source performance monitoring'
				instruction 'Bundle-DocURL', 'http://www.stagemonitor.org'
			}
		}
	}

	configurations {
		provided
		compile.extendsFrom provided

		optional
		compile.extendsFrom optional
	}

	repositories {
		mavenCentral()
		jcenter()
	}

	dependencies {
		testCompile 'junit:junit:4.12'
		testCompile 'org.mockito:mockito-core:2.10.0'
		testCompile 'ch.qos.logback:logback-classic:1.2.3'
		testCompile 'org.apache.logging.log4j:log4j-core:2.9.1'
		testCompile 'commons-io:commons-io:2.5'
		testCompile 'org.assertj:assertj-core:3.8.0'
	}

	test.testLogging.exceptionFormat = 'full'

	compileJava {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
		options.debug = true
		options.debugOptions.debugLevel = "source,lines,vars"
		options.incremental = true
	}

	compileTestJava {
		options.debugOptions.debugLevel = "source,lines,vars"
		options.incremental = true
	}

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		options.incremental = true
	}

	cobertura {
		coverageFormats = []
		coverageIgnoreTrivial = true
		coverageExcludes = ['.*org.stagemonitor.web.servlet.util.AntPathMatcher.*',
							'.*org.stagemonitor.core.util.InetAddresses.*',
							'.*org.stagemonitor.core.util.Ints.*',
							'.*org.stagemonitor.core.util.Assert.*',
							'.*org.stagemonitor.core.instrument.WeakConcurrentMap.*']
	}

	processResources {
		doLast {
			sourceSets.main.output.resourcesDir.mkdirs()
			def versionDir = file("${sourceSets.main.output.resourcesDir}/org/stagemonitor")
			versionDir.mkdirs()
			def versionFile = new File(versionDir, "version.properties")
			versionFile.text = "version=$version"
		}
		eachFile { resource ->
			def filePath = resource.file.canonicalPath
			def isGrafanaDashboard = filePath.endsWith(".json") && filePath.contains("grafana/") && filePath.contains("Elasticsearch") && !resource.path.contains("Grafana1")
			if (isGrafanaDashboard) {
				def documentationPath = filePath.replace(".json", "Documentation.md")
				def documentationFile = new File(documentationPath)
				if (documentationFile.exists()) {
					def documentationContent = """$documentationFile.text

---

[Edit this page](https://github.com/stagemonitor/stagemonitor/edit/master/${rootProject.relativePath(documentationFile)})
"""
					filter { line ->
						line.replace('$documentation', StringEscapeUtils.escapeJavaScript(documentationContent))
					}
				}
			}
		}
	}

	task copyToLib( type: Copy ) {
		into "$buildDir/libs/lib"
		from configurations.runtime - configurations.provided
	}

}

release {
	preTagCommitMessage = 'release '
	tagCommitMessage = 'release '
	newVersionCommitMessage = 'next development version '
	git {
		requireBranch = null
	}
}

def getProjectsToRelease() {
	subprojects.findAll { !(it.toString() =~ "benchmark|byte-buddy|grafana-elasticsearch|tracing-zipkin") }
}

test.dependsOn << projectsToRelease*.tasks*.withType(Test).flatten()


cobertura {
	coverageMergeDatafiles =  projectsToRelease.collect { file("${it.buildDir.path}/cobertura/cobertura.ser") }
	coverageFormats = ['html', 'xml']
	coverageIgnoreTrivial = true
	coverageSourceDirs = [projectsToRelease.sourceSets.main.java.srcDirs.flatten() ]
	coverageExcludes = ['.*org.stagemonitor.web.servlet.util.AntPathMatcher.*',
						'.*org.stagemonitor.core.util.InetAddresses.*',
						'.*org.stagemonitor.core.util.Ints.*',
						'.*org.stagemonitor.core.util.Assert.*',
						'.*org.stagemonitor.core.instrument.WeakConcurrentMap.*']
}

wrapper {
	gradleVersion = "5.5.1"
	distributionType = "all"
}

task printProjectsToTest() {
	doLast {
		println getProjectsToRelease()
	}
}

install.dependsOn subprojects.compileJava
